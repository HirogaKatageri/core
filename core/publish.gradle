apply from: '../buildSystem/versions.gradle'

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.hirogakatageri'
                artifactId = 'core'
                version = projectVersions.version_name

                pom {
                    name = 'Core'
                    description = 'A helper library for Koin-MVVM-Coroutines.'

                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://github.com/HirogaKatageri/sandbox/blob/master/LICENSE.md'
                        }
                    }

                    developers {
                        developer {
                            id = 'HirogaKatageri'
                            name = 'Gian Patrick Quintana'
                            email = 'gianpatrick27@gmail.com'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GithubPackages"
                url = "https://maven.pkg.github.com/hirogakatageri/core"
                credentials {
                    username = System.getenv("GITHUB_USER")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}